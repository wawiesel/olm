=t-depl parm=(centrm, addnux={{static.addnux}})
PWR Westinghouse 17x17, 2D quarter assembly model
{{static.xslib}}
' ----------------------------------------------------------------
'  parameters are: {{comp.uo2.iso.u235}}       - wt% U235
'                  {{comp.uo2.iso.u234}}       - wt% U234
'                  {{comp.uo2.iso.u236}}       - wt% U236
'                  {{comp.uo2.iso.u238}}       - wt% U238
'                  {{state.coolant_density}} - coolant density (g/cc)
'                  {{state.specific_power}} - average specific power
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log]
'    Rev 0: Generated by J. Doe |
'    Rev 1: Generated by B. R. Betzler, June 2014
'    Rev 2: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Pressurized Water Reactor Westinghouse 17x17
'  [Model Info] 2D t-depl quarter assembly model (see Figure \ref{fi:w17x17}), {{static.xslib}} cross-section library
'  [Sources]
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. |
'    2. SCALE: A Comprehensive Modeling and Simulation Suite for Nuclear Safety Analysis and Design, ORNL/TM-2005/39, Version 6.1, Oak Ridge National Laboratory, Oak Ridge, Tennessee, June 2011. |
'    3. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info]
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1.
'    Temperatures, moderator density, boron concentration from Table D1.A.2 of Reference 2.
'    All other dimensions, materials, etc. from Reference 3.
'  figure{w17x17.pdf: PWR Westinghouse 17x17 fuel assembly.}
' ----------------------------------------------------------------
read comp
'
' fuel - 95% theoretical density (10.96g/cc)
  uo2   1 den={{comp.density}} 1 900 92234 {{comp.uo2.iso.u234}}
                           92235 {{comp.uo2.iso.u235}}
                           92236 {{comp.uo2.iso.u236}}
                           92238 {{comp.uo2.iso.u238}} end
'
' clad
  zirc4 2 1 622 end
'
' coolant - water with {{state.ppm_boron}} ppm B
  h2o   3 den={{state.coolant_density}} 1.000000 575 end
  boron 3 den={{state.coolant_density}} {{state.ppm_boron/1.0E6}} 575 end
'
' gap - 275-500psig (set at minimum)
  he-4  4 den=0.001467 1 622 end
'
' guide tube
  zirc4 5 1 575 end
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.81915 1
                          gapd= 0.83566 4
                          cladd=0.94996 2
                          pitch=1.25984 3 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion
  1
end depletion
'
' ----------------------------------------------------------------
read burndata
  {%- for pb in time.burndata %}
  power={{pb.power}} burn={{pb.burn}} down=0 end
  {%- endfor %}
end burndata
'
' ----------------------------------------------------------------
read model
  PWR Westinghouse 17x17
'
read parm
  cmfd=0
  drawit=yes
  echo=yes
  epsinner=-1e-4
  fillmix=3
  run=yes
  xycmfd=2
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1 pn=1 com='fuel'       end
  mix=2 pn=1 com='clad'       end
  mix=3 pn=2 com='coolant'    end
  mix=4 pn=0 com='gap'        end
  mix=5 pn=1 com='guide tube' end
end materials
'
read geom
unit 1
com='fuel rod'
  cylinder 10   0.409575
  cylinder 20   0.41783
  cylinder 30   0.47498
  cuboid   40 4p0.62992
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 5
com='guide tube'
  cylinder 10   0.57175
  cylinder 20   0.61214
  cuboid   40 4p0.62992
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 11
com='1/2 fuel rod - right'
  cylinder 10 0.409575 chord +x=0
  cylinder 20 0.41783  chord +x=0
  cylinder 30 0.47498  chord +x=0
  cuboid   40 0.62992  0.0 2p0.62992
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 12
com='1/2 fuel rod - top'
  cylinder 10   0.409575 chord +y=0
  cylinder 20   0.41783  chord +y=0
  cylinder 30   0.47498  chord +y=0
  cuboid   40 2p0.62992  0.62992 0.0
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 51
com='1/2 guide tube - right'
  cylinder 10 0.5715  chord +x=0
  cylinder 20 0.61214 chord +x=0
  cuboid   40 0.62992 0.0 2p0.62992
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 52
com='1/2 guide tube - top'
  cylinder 10   0.5715  chord +y=0
  cylinder 20   0.61214 chord +y=0
  cuboid   40 2p0.62992 0.62992 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 53
com='1/4 instrument tube - NE'
  cylinder 10 0.5715  chord +x=0 chord +y=0
  cylinder 20 0.61214 chord +x=0 chord +y=0
  cuboid   40 0.62992 0.0 0.62992 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
global unit 10
com='1/4 assembly - NE'
  cuboid 10 10.75182 0.0 10.75182 0.0
  array 1 10 place 1 1 0 0
  media 3 1 10
  boundary 10 54 54
end geom
'
read array
  ara=1 nux=9 nuy=9 typ=cuboidal
  fill
  53  12  12  52  12  12  52  12  12
  11  1   1   1   1   1   1   1   1
  11  1   1   1   1   1   1   1   1
  51  1   1   5   1   1   5   1   1
  11  1   1   1   1   1   1   1   1
  11  1   1   1   1   5   1   1   1
  51  1   1   5   1   1   1   1   1
  11  1   1   1   1   1   1   1   1
  11  1   1   1   1   1   1   1   1  end fill
end array
'
read bnds
  all=mirror
end bnds
'
end model
end
